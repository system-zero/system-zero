import ("sh")
import ("dir")
import ("path")
import ("sys")
import ("file")
import ("time")
include ("argparse")

var argparse = New Argparse (3, 0, "TestLibc [arg, ...]")
argparse.add ("cc",  0, "cc=",  "set cc (default gcc)", StringType, ARG_VALUE_REQUIRED; defval : "gcc")
argparse.add ("std", 0, "std=", "set standard c version (default c11)", StringType, ARG_VALUE_REQUIRED; defval : "c11")
argparse.add ("unit", 0, "unit=", "run only this unit", StringType, ARG_VALUE_REQUIRED);

if notok is argparse.process (__argv, 0) then exit (1)

if argparse.exit then exit (0)

const CC   = argparse.results.cc
const STD  = argparse.results.std
const INCDIR = "SYSDIR": sys_get (): path_concat ("include"): path_concat ("z") 
const SRCDIR = Sys.get ("SRCDIR")
const LIBCTESTDIR = SRCDIR: path_concat ("y/pkg/libc/libc/tests")

#if notok is LIBCTESTDIR: dir_change () {
#  println (stderr, "${LIBCTESTDIR): cannot change directory: ${errno_string (errno)}")
#  exit (1)
#}

const testsFile = LIBCTESTDIR: path_concat ("tests")
const buildDir  = LIBCTESTDIR: path_concat ("build")
const logFile   = buildDir: path_concat ("logfile.lai")

ifnot testsFile: file_exists () {
  println (stderr, "${testsFile}: is missing")
  exit (1)
}

ifnot buildDir: file_exists () {
  if notok is buildDir: dir_make_parents (0700) {
    println (stderr, "${buildDir}: can not make directory: ${errno_string (errno)}")
    exit (1)
  }
}

ifnot buildDir: dir_is_directory () {
  println (stderr, "${buildDir}: is not a directory")
  exit (1)
}

if logFile: file_exists () {
  if notok is logFile: file_rename (logFile + ".old"; force : 1) {
    println (stderr, "fatal error: failed to rename ${logFile} to ${logFile}.old")
    exit (1)
  }
}

var compilation_opts = {
   argv : [CC, "", "-std=" + STD, "-I" + INCDIR]
   redir_stdout : logFile
   redir_stderr : logFile
   redir_stdout_mode : "a+"
   redir_stderr_mode : "a+"
   print_stderr : 1
}

append "-Wall"                                 in compilation_opts.argv
append "-Wno-override-init"                    in compilation_opts.argv

if CC is "gcc" {
  append "-Wimplicit-fallthrough"              in compilation_opts.argv
  append "-Wrestrict"                          in compilation_opts.argv
}

append "-g"                                    in compilation_opts.argv
append "-Wextra"                               in compilation_opts.argv
append "-Wshadow"                              in compilation_opts.argv
append "-Wunused-result"                       in compilation_opts.argv
append "-Wunused-function"                     in compilation_opts.argv
append "-Wunused-macros"                       in compilation_opts.argv
append "-Wsign-compare"                        in compilation_opts.argv
append "-Wpointer-arith"                       in compilation_opts.argv
append "-Wreturn-type"                         in compilation_opts.argv
append "-Wwrite-strings"                       in compilation_opts.argv
append "-Wuninitialized"                       in compilation_opts.argv
append "-Wmissing-declarations"                in compilation_opts.argv
append "-Werror-implicit-function-declaration" in compilation_opts.argv

if CC is "clang" {
  append "-Wno-unused-command-line-argument"   in compilation_opts.argv
  append "-Wno-initializer-overrides"          in compilation_opts.argv
  append "-Wno-c2x-extensions"                 in compilation_opts.argv
}

append "-o"                                    in compilation_opts.argv
append ""                                      in compilation_opts.argv

func append_to_logfile (msg) {
  return if ok is file_append (logFile, msg)
  println (stderr, "fatal error: failed to write to ${logFile}")
  exit (1)
}

func get_tests_from_file {
  var lines = File.readlines (testsFile)
  var tests = {}
  for v in lines
    tests.$(v) = 1
   return tests: keys ()
}

Type Unit {
  retval : ok
  num_tests
  unit
  init : func (name, num_tests) {
    this.unit = name
    this.num_tests = num_tests
  }
}

func get_unit_tests (unit) {
  var b = unit: file_read_num_bytes (20)
  ifnot b: eq_n ("// num-tests: ", 14) {
    println (stderr, "error: ${unit} has wrong header, awaiting '// num-tests: ' on the first line")
    exit (1)
  }

  b := advance (14)
  var n = b: to_integer ()

  return New Unit (unit, n)
}

const tests = {}

var unit_tests = get_tests_from_file ()

ifnot unit_tests: len () {
  println (stderr, "No tests found in ${testsFile} to run")
  exit (1)
}

for i, v in unit_tests {
  var n = get_unit_tests (LIBCTESTDIR: path_concat (v))
  var str = i: to_string (10)
  tests.$(str) = n
}

const now = time_now (): time_local ()

Sh = New Shell ()

const tm ="%Y_%m_%d_%H_%M_%S": time_format (now)

file_write (logFile, "import (\"std\")\n\nconst log = {\n  started : \"" + tm + "\"\n")

var execution_opts = {
  argv
}

var index = 0
var total = 0
var ntests = tests: len ()

var test_unit = argparse.results.unit

for ntests times {
  var s = tests.$(index: to_string (10))
  index++
  var cfile = s.unit
  var bname = cfile: path_basename_sans_extname ()

  ifnot null is test_unit
    continue ifnot bname: eq (test_unit);

  var output = buildDir: path_concat (bname)

  compilation_opts.argv[1] = cfile
  compilation_opts.argv[-1] = output

  append_to_logfile (format ("  ${bname} : {\n    compilation : {\n      log : `\n"))

  var retval = Sh.process (compilation_opts)

  append_to_logfile (format ("      `\n      retval : ${retval.retval}\n    }\n"))

  if retval.retval isnot ok {
    append_to_logfile ("  }\n")
    var com = compilation_opts.argv: join (" ")
    println (stderr, "error while compiling ${cfile}: ${com}")
    continue
  }

  append_to_logfile (format ("    execution : {\n      log : `\n"))

  var totalstr = total: to_string (10)
  execution_opts.argv = [output, total: to_string (10)]
  total += s.num_tests

  retval = Sh.process (execution_opts)

  append_to_logfile (format ("      `\n      retval : ${retval.retval}\n    }\n  }\n"))
}

append_to_logfile ("}\n\nprintln (\"started : ${log.started}\")\n")
append_to_logfile ("for k, v in log {\n")
append_to_logfile ("  continue if k: eq (\"started\")\n")
append_to_logfile ("  println (\"unit : ${k}\")\n")
append_to_logfile ("  for key, val in v {\n")
append_to_logfile ("    println (\"  action : ${key}\")\n")
append_to_logfile ("    for kk, vv in val {\n")
append_to_logfile ("      println (\"    ${kk} : ${vv}\")\n")
append_to_logfile ("    }\n")
append_to_logfile ("    println (\"\")\n")
append_to_logfile ("  }\n")
append_to_logfile ("  println (\"\")\n}\n")

exit (0)
