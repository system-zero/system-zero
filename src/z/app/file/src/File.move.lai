var prog = "File.nove"

import ("file")
import ("dir")

loadfile ("argparse")

var argparse = New Argparse (6, 0, prog + " [option[s]] source destination")
argparse.add ("force", 'f', "force", "do not prompt before overwriting", BooleanType, 0)
argparse.add ("backup", 'b', "backup", "make a backup of each existing destination file", BooleanType, 0) 
argparse.add ("recursive", 'R', "recursive", "copy recursively", BooleanType, 0)
argparse.add ("interactive", 'i', "interactive", "prompt before overwrite", BooleanType, 0)
argparse.add ("verbose", 'v', "verbose=", "be verbose", IntegerType, ARG_VALUE_OPTIONAL|ARG_VALUE_APPEND)
argparse.add ("help", 'h', "help", "show this message", BooleanType, 0)

ifnot ok is argparse.process (__argv, 1) then exit (1)

if argparse.exit is true then exit (0)

var qualif = {
  force       : argparse.results.force
  backup      : argparse.results.backup
  recursive   : argparse.results.recursive
  interactive : argparse.results.interactive
  verbose     : argparse.results.verbose: ifnot x then 1 orelse x + 1 end
}

if (argparse.argc < 2) {
  println (stderr, "${prog}: missing operands")
  exit (1)
}

var destination = argparse.argv[argparse.argc - 1]

var is_directory = dir_is_directory

if (argparse.argc > 2) {
  ifnot (destination: is_directory ()) {
    println (stderr, "${destination} is not a directory")
    exit (0)
  }
}

var exitval = 0

var retval = ok

for (var i = 0; i < argparse.argc - 1; i += 1) {
  retval = argparse.argv[i]: file_rename (destination; qualif)
  exitval = if retval is notok then 1 orelse exitval
}

exit (exitval)
